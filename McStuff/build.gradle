/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'administrator' at '4/28/16 3:24 PM' with Gradle 2.13
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.13/userguide/tutorial_java_projects.html
 */
 
buildscript {
	ext {
		springBootVersion = '1.4.0.M2'
	}
	
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
    dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}
 
allprojects {
 	apply plugin: 'eclipse'
 	apply plugin: 'java'
 	
 	repositories {
	    jcenter()
	    mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	} 	
}
 
subprojects {		
 	apply plugin: 'spring-boot'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

 	eclipse {
 		project {
 			natures 'org.springframework.ide.eclipse.core.springnature'
			classpath {
				containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
				containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
 				containers 'org.eclipse.fx.ide.jdt.core.JAVAFX_CONTAINER'
			} 			
 		}
 	}
	
	jar {
		version = '0.0.1-SNAPSHOT'
	}
	
	// In this section you declare where to find the dependencies of your project
	
	// In this section you declare the dependencies for your production and test code
	dependencies {
	    compile 'org.springframework.boot:spring-boot-starter'
		compile 'org.springframework.boot:spring-boot-starter-data-jpa'

	    compile 'org.apache.commons:commons-lang3:3+'	    
		
		runtime 'org.apache.derby:derby:10+'

	
	    // Declare the dependency for your favourite test framework you want to use in your tests.
	    // TestNG is also supported by the Gradle Test task. Just change the
	    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
	    // 'test.useTestNG()' to your build script.
	    testCompile 'junit:junit:4.12'
	    testCompile 'org.springframework.boot:spring-boot-starter-test'	    
	}
}

project(':McStuff_app') {
	apply plugin: 'application'
	
	eclipse {
 		project {
			classpath {
 				containers 'org.eclipse.fx.ide.jdt.core.JAVAFX_CONTAINER'
			} 			
 		}
 	}
	
	mainClassName = "mcstuff.application.Application"
	
	dependencies {
		compile project(':McStuff_moduleApi')
		compile project(':McStuff_rpg_module')
		compile project(':McStuff_bbs_module')
	}
	
	
}

project(':McStuff_rpg_module') {
	eclipse {
 		project {
			classpath {
 				containers 'org.eclipse.fx.ide.jdt.core.JAVAFX_CONTAINER'
			} 			
 		}
 	}

	dependencies {
		compile project(':McStuff_moduleApi')
	}

}

project(':McStuff_bbs_module') {
	eclipse {
 		project {
			classpath {
 				containers 'org.eclipse.fx.ide.jdt.core.JAVAFX_CONTAINER'
			} 			
 		}
 	}

	dependencies {
		compile project(':McStuff_moduleApi')
	}
}

project(':McStuff_web') {
	apply plugin: 'application'

	mainClassName = "mcstuff.web.McWebStuffApplication"

	dependencies {
		compile('org.springframework.boot:spring-boot-starter-data-rest')
		compile('org.springframework.boot:spring-boot-starter-security')
		compile('org.springframework.boot:spring-boot-starter-web')				
	}
}

dependencies {
	compile project(':McStuff_app')
	compile project(':McStuff_moduleApi')
	compile project(':McStuff_bbs_module')
	compile project(':McStuff_rpg_module')
	compile project(':McStuff_web')
}


